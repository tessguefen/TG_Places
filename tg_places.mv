<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvASSIGN NAME = "l.module:code"		VALUE = "tg_places">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Super Fantastic Find Us Locator!">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Store Place ID information to serve up on the site.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "system, vis_system, component, data_store, json, clientside, import">
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	INSTALL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCOMMENT>
	===========================
	Create TG_Places Table
	===========================
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGPlaces', 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TG-PLACES-1000:', 'An error occured while creating the storekey, TGPlaces. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'TG_Places
						  (
						  	id					' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 )	$ ',
							place_id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
							name				' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
							formatted_address	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
							geometry			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
							type				' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
							active				' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 )	$ ',
							last_updated		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 )	$ '
						  ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGPlaces' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TG-PLACES-1001:', 'An error occured while creating the table TG_Places. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TG_Places ON ' $ g.Store_Table_Prefix $ 'TG_Places ( place_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGPlaces' ) }">
		<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TG_Places' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TG-PLACES-1002:', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'TG_Places_Settings
						  (
							apikey				' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
							auto_active			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 )	$ '
						  ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGPlaces' ) }">
		<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TG_Places' }">
		<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TG_Places_Settings' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TG-PLACES-1003:', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Item_Load_Code( 'tg_places', l.null ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'tg_places', l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

	</MvIF>

	<MvASSIGN NAME = "l.google_places_file_path" VALUE = "{ g.domain:mod_root $ 'js/google-places.js' }">
	<MvIF EXPR = "{ NOT sexists( l.google_places_file_path ) }">
		<MvCAPTURE VARIABLE = "l.google_places_contents">
			<MvINCLUDE FILE = "js/googleplaces.js">
		</MvCAPTURE>
		<MvASSIGN NAME = "l.create" VALUE = "{ file_create( l.google_places_file_path, 'script', l.google_places_contents ) }">
		<MvASSIGN NAME = "l.file_perms" VALUE = "{ fchmod( l.google_places_file_path, '0755' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGPlaces' ) }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TG_Places' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TG_Places_Settings' }">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	SYSTEM
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, exception" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	VIS_SYSTEM
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_System_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvASSIGN NAME = "l.success" VALUE = "{ Place_Settings( l.tg_place_setup ) }">
	<MvIF EXPR = "{ l.tg_place_setup:apikey }">
		<MvFUNCTIONRETURN VALUE = "TG_PLACES:Places,TG_PLACES_SETTINGS:Places Settings">
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "TG_PLACES_SETTINGS:Places Settings">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.tab EQ 'TG_PLACES'}">
		<MvASSIGN NAME = "l.success" VALUE = "{ Place_Settings( l.setup ) }">
		<MvIF EXPR = "{ l.setup:auto_active EQ 1 }">
			<MvASSIGN NAME = "g.auto_active" VALUE = "1" />
		<MvELSE>
			<MvASSIGN NAME = "g.auto_active" VALUE = "0" />
		</MvIF>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMDialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMDialog_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=TGPlaces_MapPopup.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=TGPlaces_functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=TGPlaces.js&auto_active=' $ g.auto_active }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new TGPlaces_Batchlist(); } );
		</script>
		<style>#TGPlaces_map{height: 100% !important;}</style>
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TG_PLACES_SETTINGS' }">
		<style>
			#TGPlaces_Settings .input,
			#TGPlaces_Settings select,
			#TGPlaces_Settings textarea {
				padding: 5px;
				margin-right: .5rem;
				border: 1px solid #ccc;
				width: 100%;
				min-width: 250px;
				box-sizing: border-box;
			}
		</style>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.tab EQ 'TG_PLACES' }">
		<MvASSIGN NAME = "l.success" VALUE = "{ Place_Settings( l.setup ) }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">

		<div id="TGPlaces_Batchlist_id"></div>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMDialog_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].MMDialog_Begin( 'tg_places_googlemap' ) }">
			<iframe id="GoogleMap" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Map.html&key=' $ l.setup:apikey }" sandbox="allow-scripts allow-forms" style="width: 100%;height: 100%;border: 0;margin: 0;padding: 0;"></iframe>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].MMDialog_End( 'tg_places_googlemap' ) }">
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TG_PLACES_SETTINGS' }">
		<MvASSIGN NAME = "l.success" VALUE = "{ Place_Settings( l.setup ) }">
		<MvIF EXPR = "{ ISNULL g.TGPlaces_apikey }">
			<MvASSIGN NAME = "g.TGPlaces_apikey" VALUE = "{ l.setup:apikey }">
		</MvIF>
		<MvIF EXPR = "{ ISNULL g.TGPlaces_auto_active }">
			<MvASSIGN NAME = "g.TGPlaces_auto_active" VALUE = "{ l.setup:auto_active }">
		</MvIF>
		<div id="TGPlaces_Settings">
			<input type="hidden" name="TGPlaces_Settings_Update" value="1" />
			<table>
				<tr>
					<td align="right">API Key</td>
					<td><input type="text" class="input" name="TGPlaces_apikey" value="{ g.TGPlaces_apikey }" /></td>
				</tr>
				<tr>
					<td>&nbsp;</td>
					<td><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( g.TGPlaces_auto_active, 'TGPlaces_auto_active', 1, 'Auto Activate Places When Added' ) }">
				</tr>
			</table>
			<hr noshade="" />
			JSON Data is located here: <pre><MvEVAL EXPR = "{ g.domain:mod_root $ 'js/places.js' }"></pre>
			<hr noshade="" />
			tgplaces item...<br />
			<pre>
				&lt;mvt:item name="tg_places" param="Apikey( l.all_settings:places_key )"/&gt;
			</pre>
			This will return the Apikey to <strong>l.settings:places_key</strong>
			<pre>
				&lt;mvt:item name="tg_places" param="PlacesFile( l.all_settings:places_file )"/&gt;
			</pre>
			This will return the some variables to <strong>l.settings:places_file</strong>. Variables.. :file_path, :script_tag, :last_updated<br />
			:file_path = <MvEVAL EXPR = "{ g.domain:mod_root $ 'js/places.js' }"><br />
			:script_tag = &lt;script language="JavaScript" src="<MvEVAL EXPR = "{ g.domain:mod_root $ 'js/places.js?v=1493130172' }">"&gt;&lt;/script&gt;<br />
			:last_updated = 1493130172<br />
			<hr />
			item to output JS information (api key will be output, js file for places like BB, AND the places var file [[ TO APPEND ?v=[ time stamp]]])<br />
			<br />
			TODO:<br />
			Add a button on batchlist to update file? I could keep a record somewhere of when the file was last updated ... that way it is less calls to the DB?
		</div>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "html, text, compresswhitespace">
	<MvIF EXPR = "{ g.TGPlaces_Settings_Update EQ 1 }">
		<MvASSIGN NAME = "l.setup" MEMBER = "apikey" 			VALUE = "{ g.TGPlaces_apikey }">
		<MvASSIGN NAME = "l.setup" MEMBER = "auto_active" 	VALUE = "{ g.TGPlaces_auto_active }">
		<MvIF EXPR = "{ NOT Update_Place_Settings( l.setup ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].FieldError( 'TG_PLACES_SETTINGS', '', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvIF EXPR = "{ g.TGPlaces_Settings_Update EQ 1 }">
		<MvASSIGN NAME = "g.TGPlaces_apikey" VALUE = "{ trim( g.TGPlaces_apikey ) }">
		<MvIF EXPR = "{ ISNULL g.TGPlaces_apikey }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].FieldError( 'TG_PLACES_SETTINGS', 'TGPlaces_apikey', 'Please Specify an API Key.' ) }">
		</MvIF>
		<MvASSIGN NAME = "g.TGPlaces_auto_active" VALUE = "{ trim( g.TGPlaces_auto_active ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	COMPONENT
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "compresswhitespace">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "compresswhitespace">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "compresswhitespace">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "compresswhitespace">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "compresswhitespace">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.function_name EQ 'apikey' }">
		<MvIF EXPR = "{ l.parameter_count EQ 1 }">
			<MvIF EXPR = "{ NOT Is_Variable( l.parameters[1] ) }">
				<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'The third parameter must be a variable' ) }">
			</MvIF>
			<MvIF EXPR = "{ NOT Place_Settings( l.setup ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
			<MvASSIGN NAME = "{ l.parameters[1] }" VALUE = "{ l.setup:apikey }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.function_name EQ 'placesfile' }">
		<MvIF EXPR = "{ l.parameter_count EQ 1 }">
			<MvIF EXPR = "{ NOT Is_Variable( l.parameters[1] ) }">
				<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'The third parameter must be a variable' ) }">
			</MvIF>
			<MvIF EXPR = "{ NOT Place_Settings( l.setup ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
			<MvASSIGN NAME = "{ l.parameters[1] }" VALUE = "{ g.domain:mod_root $ 'js/places.js' }">
		</MvIF>
	</MvIF>
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	JSON
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Module_Function EQ 'TGPlaces_Load_Query' }">
		<MvEVAL EXPR = "{ JSON_TGPlaces_Load_Query( l.module ) }">
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Place_Insert' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Place_Insert( g.place_id, g.name, g.formatted_address, g.geometry, g.type, g.active ) }">
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Place_Update' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Place_Update( g.place_id, g.name, g.formatted_address, g.geometry, g.type, g.active ) }">
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Place_Active' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Place_Active( g.PlaceID, g.Active) }">
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Place_Delete' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Place_Delete( g.PlaceID ) }" />
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Rebuild_File' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Rebuild_File() }" />
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	CLIENTSIDE
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "html, text, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Filename EQ 'TGPlaces.js' }">
		<MvINCLUDE FILE = "js/TGPlaces.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'TGPlaces_functions.js' }">
		<MvINCLUDE FILE = "js/TGPlaces_functions.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'TGPlaces_MapPopup.js' }">
		<MvINCLUDE FILE = "js/TGPlaces_MapPopup.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Map.html' }">
		<MvIF EXPR = "{ NOT g.key }">
			Brah, wheres ur api key?
			<MvEXIT />
		</MvIF>
		<style>
			#pac-input {
				width: 60%;
				margin-top: 10px;
				border: 0;
				color: rgb(0, 0, 0);
				font-family: Roboto, Arial, sans-serif;
				font-size: 11px;
				background-color: rgb(255, 255, 255);
				padding: 8px;
				border-radius: 2px;
				-webkit-background-clip: padding-box;
				background-clip: padding-box;
				box-shadow: rgba(0, 0, 0, 0.298039) 0px 1px 4px -1px;
				font-weight: 500;
			}
		</style>
		<script language="JavaScript">
			<MvINCLUDE FILE = "js/TGPlaces_Iframe.js">
		</script>
		<input id="pac-input" class="controls" type="text" placeholder="Enter a location" />
		<div id="TGPlaces_map" style="height: 100%;"></div>
		<script language="JavaScript" src="{ 'https://maps.googleapis.com/maps/api/js?key=' $ g.key $ '&libraries=places'}" async defer></script>
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	IMPORT
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ImportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:screen"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:persistent"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:format"				VALUE = "delimited">
	<MvASSIGN NAME = "l.capabilities:persistent_provision"	VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Columns" PARAMETERS = "module var, import var, columns var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>											field					name						header				required	default			validation		</MvCOMMENT>
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'place_id',				'Place ID',					'PLACE_ID',				0,			'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'name',					'Name',						'NAME',					0,			'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'formatted_address',	'Formatted Address',		'FORMATTED_ADDRESS',	0,			'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'lat',					'Latitude',					'LATITUDE',				0,			'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'long',					'longitude',				'LONGITUDE',			0,			'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'active',				'Active',					'ACTIVE',				0,			0,			'bool' ) }">

	<MvFUNCTIONRETURN VALUE = "{ miva_array_max( l.columns ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_StatusFields" PARAMETERS = "module var, import var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields"	VALUE = "">

	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'records_processed',	'Records Processed:',	0 ) }">
	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'records_skipped',	'Records Skipped:',		0 ) }">
	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'record_problems',	'Record Problems:',		0 ) }">

	<MvFUNCTIONRETURN VALUE = "{ miva_array_max( l.fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_Begin" PARAMETERS = "module var, import var, session var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Create_Columns_Present( l.import, l.run_data:columns_present ) }">

	<MvIF EXPR = "{ l.import:automap EQ 1 }">
		<MvIF EXPR = "{ NOT Validate_Columns( l.import, l.run_data:columns_present ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

    <MvASSIGN NAME = "l.list_pos"				VALUE = 0>
    <MvFOREACH ITERATOR = "l.column" ARRAY = "l.run_data:columns_all" INDEX = "l.index" COUNT = "{ ImportModule_Delimited_Columns( l.module, l.import, l.run_data:columns_all ) }">
        <MvIF EXPR = "{ miva_variable_value( 'l.run_data:columns_present:' $ l.column:field ) }">
            <MvASSIGN NAME = "l.column:present"	VALUE = 1>

            <MvREFERENCE NAME = "l.run_data:columns_list"		INDEX = "{ ++l.list_pos }"		VARIABLE = "{ 'l.run_data:columns_all[' $ l.index $ ']' }">
            <MvREFERENCE NAME = "l.run_data:columns_present"	MEMBER = "{ l.column:field }"	VARIABLE = "{ 'l.run_data:columns_all[' $ l.index $ ']' }">
        </MvIF>
    </MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_Record" PARAMETERS = "module var, import var, session var, record var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_IMP_UT ].Import_Session_StatusField_Increment( l.session, 'records_processed', 1 ) }">

	<MvIF EXPR = "{ ISNULL l.record:place_id AND NOT( l.record:lat AND l.record:long AND l.record:name ) }">
		<MvEVAL EXPR = "{ Record_Skip( l.session, 'Place ID, or Name, Latitude & Longitude are required.' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.record:place_id }">
		<MvFUNCTIONRETURN VALUE = "{ Import_Place( l.import, l.session, l.record, l.run_data ) }">
	<MvELSEIF EXPR = "{ l.record:lat AND l.record:long }">
		<MvFUNCTIONRETURN VALUE = "{ Import_LatLong( l.import, l.session, l.record, l.run_data ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_End" PARAMETERS = "module var, import var, session var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Fields" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Validate" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.import:automap EQ 0 }">
		<MvEVAL EXPR = "{ Create_Columns_Present( l.import, l.columns_present ) }">

		<MvIF EXPR = "{ NOT Validate_Columns( l.import, l.columns_present ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Update" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Provision" PARAMETERS = "module var, import var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvINCLUDE FILE = "functions.mv">

<MvINCLUDE FILE = "includes/import_includes.mv">