<MvFUNCTION NAME = "Place_Settings" PARAMETERS = "setup var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "TGPLACESSETTINGS"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TG_Places_Settings' }">

	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACESSETTINGS">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1007:', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGPLACESSETTINGS.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACESSETTINGS">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'TGPLACES-FUNC-1004' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.setup"	MEMBER = "apikey"			VALUE = "{ TGPLACESSETTINGS.d.apikey }">
	<MvASSIGN NAME = "l.setup"	MEMBER = "auto_active"		VALUE = "{ TGPLACESSETTINGS.d.auto_active }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACESSETTINGS">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Update_Place_Settings" PARAMETERS = "setup var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Place_Settings( l.check_setup ) }">
		<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TG_Places_Settings
						  ( apikey, auto_active )
						  VALUES
						  ( ?, ?)' }"
			 FIELDS = "l.setup:apikey, l.setup:auto_active">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1009', g.MvQUERY_Error ) }">
		</MvIF>
	<MvELSE>
		<MvQUERY NAME 	= "Merchant"
				 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TG_Places_Settings
								SET
									apikey = ?,
									auto_active = ?
								WHERE 1' }"
				 FIELDS = "l.setup:apikey, l.setup:auto_active">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1008', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>	
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_TGPlaces_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Filter" VALUE = "{ trim( g.Filter ) }">
	<MvASSIGN NAME = "g.Sort" VALUE = "{ trim( g.Sort ) }">
	<MvASSIGN NAME = "g.Offset" VALUE = "{ trim( g.Offset ) }">
	<MvASSIGN NAME = "g.Count" VALUE = "{ trim( g.Count ) }">
	<MvASSIGN NAME = "l.search_query" VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TG_Places', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, g.Filter,'place_id:s.place_id,name:s.name,formatted_address:s.formatted_address,geometry:s.geometry,type:s.type,active:s.active,last_updated:s.last_updated' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields(l.search_query, g.Sort, 'place_id:s.place_id,name:s.name,formatted_address:s.formatted_address,geometry:s.geometry,type:s.type,active:s.active,last_updated:s.last_updated', 's.place_id' ) }">

	<MvIF EXPR = "{ g.Active EQ 1 AND g.Inactive EQ 0 }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 's.active = ?', 'g.Active' ) }">
	<MvELSEIF EXPR = "{ g.Inactive EQ 1 AND g.Active EQ 0 }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 's.active = ?', 'g.Active' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, '1', '' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error(g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'TGPLACES', l.search_sql, l.search_fields, g.Offset, g.Count) }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TGPLACES-FUNC-1001',g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
		{
			"data":
				[
					<MvWHILE EXPR = "{ ( NOT TGPLACES.d.EOF ) AND ( ( g.Count EQ 0 ) OR (l.count LT g.Count ) ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.count )}">
					"id" : <MvEVAL EXPR = "{ int( TGPLACES.d.id ) }">,
					"place_id" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGPLACES.d.place_id ) }">",
					"name" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGPLACES.d.name ) }">",
					"formatted_address" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGPLACES.d.formatted_address ) }">",
					"geometry" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGPLACES.d.geometry ) }">",
					"last_updated" : <MvEVAL EXPR = "{ int( TGPLACES.d.last_updated) }">,
					"type" : "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( TGPLACES.d.type ) }">",
					"active" : <MvEVAL EXPR = "{ int( TGPLACES.d.active ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					<MvSKIP NAME = "Merchant" VIEW = "TGPLACES" ROWS = 1>
					</MvWHILE>
				],

			"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">,
			"start_offset": <MvEVAL EXPR = "{ int( g.Offset ) }">
		}
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">
	}
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Place_Insert" PARAMETERS = "place_id, name, formatted_address, geometry, type, PlaceActive" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Place_Insert( l.place_id, l.name, l.formatted_address, l.geometry, l.type, l.PlaceActive, l.errors ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( l.errors:json_code, l.errors:message ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Place_Insert" PARAMETERS = "place_id, name, formatted_address, geometry, type, active, errors var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.place_id" VALUE = "{ trim( l.place_id ) }">

	<MvIF EXPR = "{ ISNULL l.place_id }">
		<MvASSIGN NAME = "l.errors:json_code" VALUE = "{ 'place_id' }">
		<MvASSIGN NAME = "l.errors:message" VALUE = "{ 'Place ID can not be empty' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ Load_Place( l.place_id, l.place ) }">
		<MvASSIGN NAME = "l.errors:json_code" VALUE = "place_id">
		<MvASSIGN NAME = "l.errors:message" VALUE = "{ 'This place ID already exists.' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Google_Places_Get_Place( l.place_id, l.place, l.errors ) }">
		<MvASSIGN NAME = "l.errors:json_code" VALUE = "place_id">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.name }">
		<MvASSIGN NAME = "l.name" VALUE = "{ l.place:name }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.formatted_address }">
		<MvASSIGN NAME = "l.formatted_address" VALUE = "{ l.place:formatted_address }">
	</MvIF>

	<MvASSIGN NAME = "l.place:geometry" VALUE = "{ miva_array_serialize( l.place:geometry ) }">

	<MvASSIGN NAME = "l.id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGPlaces' ) }">

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TG_Places
						  ( id, place_id, name, formatted_address, geometry, type, active, last_updated )
						  VALUES
						  ( ?, ?, ?, ?, ?, ?, ?, ?)' }"
			 FIELDS = "l.id, l.place_id, l.name, l.formatted_address, l.place:geometry, l.type, l.active, s.dyn_time_t">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.errors:json_code" VALUE = "place_id">
		<MvASSIGN NAME = "l.errors:message" VALUE = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1002:', g.MvQUERY_Error ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MVFUNCTION NAME = "JSON_Place_Update" PARAMETERS = "place_id, name, formatted_address, geometry, type, active" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ Place_Update( l.place_id, l.name, l.formatted_address, l.geometry, l.type, l.active, l.errors ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( l.errors:json_code, l.errors:message ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Place_Update" PARAMETERS = "place_id, name, formatted_address, geometry, type, active, errors var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.place_id" VALUE = "{ trim( l.place_id ) }">

	<MvIF EXPR = "{ ISNULL l.place_id }">
		<MvASSIGN NAME = "l.errors:json_code" VALUE = "{ 'place_id' }">
		<MvASSIGN NAME = "l.errors:message" VALUE = "{ 'Place ID can not be empty' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Load_Place( l.place_id, l.place ) }">
		<MvASSIGN NAME = "l.errors:json_code" VALUE = "place_id">
		<MvASSIGN NAME = "l.errors:message" VALUE = "{ 'This place ID does not exists.' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TG_Places
			 				SET
				 				name = ?,
				 				formatted_address = ?,
				 				geometry = ?,
				 				type = ?,
				 				active = ?,
				 				last_updated = ?
						  WHERE
						  	place_id = ?' }"
			 FIELDS = "l.name, l.formatted_address, l.geometry, l.type, l.active, s.dyn_time_t, l.place_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.errors:json_code" VALUE = "place_id">
		<MvASSIGN NAME = "l.errors:message" VALUE = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1007:', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Place" PARAMETERS = "place_id, place var" STANDARDOUTPUTLEVEL = "compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.place_id" VALUE = "{ trim( l.place_id ) }">
	<MvIF EXPR = "{ ISNULL l.place_id }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "TGPLACES"
				QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TG_Places WHERE place_id = ?' }"
				FIELDS 	= "l.place_id">

	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1003:', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGPLACES.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'TGPLACES-FUNC-1004' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.place"	MEMBER = "place_id"				VALUE = "{ TGPLACES.d.place_id }">
	<MvASSIGN NAME = "l.place"	MEMBER = "name"					VALUE = "{ TGPLACES.d.name }">
	<MvASSIGN NAME = "l.place"	MEMBER = "formatted_address"	VALUE = "{ TGPLACES.d.formatted_address }">
	<MvASSIGN NAME = "l.place"	MEMBER = "geometry"				VALUE = "{ TGPLACES.d.geometry }">
	<MvASSIGN NAME = "l.place"	MEMBER = "type"					VALUE = "{ TGPLACES.d.type }">
	<MvASSIGN NAME = "l.place"	MEMBER = "active"				VALUE = "{ TGPLACES.d.active }">
	<MvASSIGN NAME = "l.place"	MEMBER = "last_updated"			VALUE = "{ TGPLACES.d.last_updated }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Place_Active" PARAMETERS = "place_id, active" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Place_Active( l.place_id, l.active ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'active', g.MvQUERY_Error ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "Place_Active" PARAMETERS = "place_id, active" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.place_id" VALUE = "{ trim( l.place_id ) }">
	<MvASSIGN NAME = "l.active" VALUE = "{ trim( l.active ) }">

	<MvIF EXPR = "{ NOT Load_Place( l.place_id, l.place ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.active NE 1 }">
		<MvASSIGN NAME = "l.active" VALUE = "0">
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TG_Places
							SET
								active = ?
							WHERE
								place_id = ?' }"
			 FIELDS = "l.active, l.place_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Place_Delete" PARAMETERS = "place_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Place_Delete( l.place_id ) }">	
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'place_id', g.MvQUERY_Error ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "Place_Delete" PARAMETERS = "place_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.place_id" VALUE = "{ trim( l.place_id ) }">

	<MvIF EXPR = "{ ISNULL l.place_id }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TG_Places WHERE place_id = ?' }"
			 FIELDS = "l.place_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1006', g.MvQUERY_Error ) }">
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Google_Places_Get_Place" PARAMETERS = "place_id, place var, errors var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.place_id" VALUE = "{ trim( l.place_id ) }">

	<MvIF EXPR = "{ ISNULL l.place_id }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Place_Settings( l.setup ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.setup:apikey }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCALL ACTION = "{ 'https://maps.googleapis.com/maps/api/place/details/json?placeid=' $ l.place_id $ '&key=' $ l.setup:apikey }" METHOD = "GET">
		<MvASSIGN NAME = "l.maps_response" VALUE = "{ l.maps_response $ s.callvalue }">
	</MvCALL>

	<MvASSIGN NAME = "l.success" VALUE = "{ miva_json_decode( l.maps_response, l.place_info ) }">

	<MvIF EXPR = "{ l.place_info:status NE 'OK' }">
		<MvASSIGN NAME = "l.errors:message" VALUE = "{ l.place_info:status }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.place_info:result:name }">
		<MvASSIGN NAME = "l.errors:message" VALUE = "'## ERROR ##'">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.place" MEMBER = "name" 				VALUE = "{ l.place_info:result:name }">
	<MvASSIGN NAME = "l.place" MEMBER = "formatted_address" VALUE = "{ l.place_info:result:formatted_address }">
	<MvASSIGN NAME = "l.place" MEMBER = "geometry" 			VALUE = "{ l.place_info:result:geometry }">
	<MvASSIGN NAME = "l.place" MEMBER = "id"				VALUE = "{ l.place_id }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGPlace_JSON_Output" PARAMETERS = "value var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "l.member_count"	VALUE = "{ miva_struct_members( l.value, l.members ) }">
	<MvIF EXPR = "{ l.member_count GT 0 }">
		{
			<MvFOREACH INDEX = "l.pos" ITERATOR = "l.member" ARRAY = "l.members" COUNT = "{ l.member_count }">
				<MvIF EXPR = "{ l.pos GT 1 }">
					,
				</MvIF>

				<MvREFERENCEARRAY NAME = "l.member_value" VARIABLE = "l.value">
					<MvMEMBER NAME = "{ l.member }">
				</MvREFERENCEARRAY>

				"<MvEVAL EXPR = "{ encodejavascriptstring( l.member ) }">": <MvEVAL EXPR = "{ TGPlace_JSON_Output( l.member_value ) }">
			</MvFOREACH>
		}

		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.element_count"	VALUE = "{ miva_array_elements( l.value ) }">
	<MvIF EXPR = "{ l.element_count GT 0 }">
		[
		<MvFOREACH INDEX = "l.pos" ITERATOR = "l.element" ARRAY = "l.value" COUNT = "{ l.element_count }">
			<MvIF EXPR = "{ l.pos GT 1 }">
				,
			</MvIF>

			<MvEVAL EXPR = "{ TGPlace_JSON_Output( l.element ) }">
		</MvFOREACH>
		]

		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.value_nodec" VALUE = "{ gettoken( l.value, '.', 1 ) }">
	<MvASSIGN NAME = "l.value_negative" VALUE = "{ l.value_nodec * -1 }">

	<MvIF EXPR = "{ ( isdigit( l.value ) OR isdigit( l.value_nodec ) OR isdigit( l.value_negative) ) AND
					NOT ISNULL l.value	AND
					NOT ( len_var( l.value ) GE 2 AND substring_var( l.value, 1, 1 ) EQ '0' ) }">
		<MvEVAL EXPR = "{ l.value }">
	<MvELSE>
		"<MvEVAL EXPR = "{ encodejavascriptstring( l.value ) }">"
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Rebuild_File" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Rebuild_Places_File() }">	
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( '', 'Error rebuilding the file.' ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "Rebuild_Places_File" ERROROUTPUTLEVEL = "" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.file_path" VALUE = "{ g.domain:mod_root $ 'js/places.js' }">

	<MvIF EXPR = "{ NOT Places_JSON_Contents( l.file_contents ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ sexists( l.file_path ) }">
		<MvASSIGN NAME = "l.delete" VALUE = "{ sdelete( l.file_path ) }">
	</MvIF>

	<MvASSIGN NAME = "l.create" VALUE = "{ file_create( l.file_path, 'script', l.file_contents ) }">

	<MvASSIGN NAME = "l.file_perms" VALUE = "{ fchmod( l.file_path, '0755' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Places_JSON_Contents" PARAMETERS = "file_contents var" STANDARDOUTPUTLEVEL = "compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "TGPLACES"
				QUERY 	= "{ 'SELECT place_id, name, formatted_address, geometry FROM ' $ g.Store_Table_Prefix $ 'TG_Places WHERE active = 1' }">

	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGPLACES-FUNC-1003:', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGPLACES.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'TGPLACES-FUNC-1004' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = "1">

	<MvWHILE EXPR = "{ NOT TGPLACES.d.EOF }">
		<MvASSIGN NAME = "l.places"	INDEX ="{ l.count }"	MEMBER = "place_id"				VALUE = "{ TGPLACES.d.place_id }">
		<MvASSIGN NAME = "l.places"	INDEX ="{ l.count }"	MEMBER = "name"					VALUE = "{ TGPLACES.d.name }">
		<MvASSIGN NAME = "l.places"	INDEX ="{ l.count }"	MEMBER = "formatted_address"	VALUE = "{ TGPLACES.d.formatted_address }">
		<MvASSIGN NAME = "l.places"	INDEX ="{ l.count }"	MEMBER = "geometry"				VALUE = "{ miva_array_deserialize( TGPLACES.d.geometry ) }">
		<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">
		<MvSKIP NAME = "Merchant" VIEW = "TGPLACES" ROWS = 1>
	</MvWHILE>
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGPLACES">

	<MvCAPTURE VARIABLE = "l.file_contents">
		<MvEVAL EXPR = "{ TGPlace_JSON_Output( l.places ) }">
	</MvCAPTURE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Import_Get_Place" PARAMETERS = "name, lat, long, formatted_address, active, errors var" STANDARDOUTPUTLEVEL = "compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.lat" 	VALUE = "{ trim( l.lat ) }">
	<MvASSIGN NAME = "l.long"	 VALUE = "{ trim( l.long ) }">
	<MvASSIGN NAME = "l.name"	 VALUE = "{ trim( l.name ) }">
	<MvIF EXPR = "{ ISNULL l.long OR ISNULL l.lat OR ISNULL l.name }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Place_Settings( l.setup ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.setup:apikey }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.maps_response" VALUE = "">

	<MvCALL ACTION = "{ 'https://maps.googleapis.com/maps/api/place/nearbysearch/json?location=' $ l.lat $ ',' $ l.long $ '&radius=5&keyword=' $ l.name $ '&key=' $ l.setup:apikey }" METHOD = "GET">
		<MvASSIGN NAME = "l.maps_response" VALUE = "{ l.maps_response $ s.callvalue }">
	</MvCALL>

	<MvASSIGN NAME = "l.success" VALUE = "{ miva_json_decode( l.maps_response, l.place_info ) }">

	<MvIF EXPR = "{ l.place_info:status NE 'OK' }">
		<MvASSIGN NAME = "l.errors" VALUE = "{ l.place_info:status }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.place_info" VALUE = "{ l.place_info:results[1] }">

	<MvIF EXPR = "{ NOT Place_Insert( l.place_info:place_id, l.name, l.formatted_address, '', 'Import', l.active, l.errors ) }">
		<MvASSIGN NAME = "l.errors" VALUE = "{ l.errors:message }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvCOMMENT>
===
	Component helpers!!!
===
</MvCOMMENT>

<MvFUNCTION NAME = "Parse_Function_Parameters" PARAMETERS = "string, function_name var, parameters var, parameter_count var" STANDARDOUTPUTLEVEL = "">	
	<MvASSIGN NAME = "l.parameter_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.loop_counter" 		VALUE = 0>
	<MvASSIGN NAME = "l.string" 			VALUE = "{ trim( l.string ) }">
	<MvASSIGN NAME = "l.starting_pos" 		VALUE = "{ indexof( '(', l.string, 1 ) + 1 }">
	<MvASSIGN NAME = "l.function_name" 		VALUE = "{ tolower( substring_var( l.string, 1, l.starting_pos - 2 ) ) }">
	
	<MvIF EXPR = "{ l.starting_pos EQ 1 }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Expected \'(\' to start the function call' ) }">
	</MvIF>
	
	<MvIF EXPR = "{ ISNULL l.function_name }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Missing function name' ) }">
	</MvIF>
	
	<MvIF EXPR = "{ substring_var( l.string, len_var( l.string ), 1 ) NE ')' }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Expected \')\' to end the function call' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ 1 }">
		<MvASSIGN NAME = "l.quote_pos" 		VALUE = "{ indexof( '\'', l.string, l.starting_pos ) }">
		<MvASSIGN NAME = "l.end_quote_pos" 	VALUE = "{ indexof( '\'', l.string, l.quote_pos + 1 ) }">
		<MvASSIGN NAME = "l.comma_pos" 		VALUE = "{ indexof( ',', l.string, l.starting_pos ) }">

		<MvIF EXPR = "{ l.quote_pos AND NOT l.end_quote_pos }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Missing single quote' ) }">
		</MvIF>
		
		<MvWHILE EXPR = "{ ( l.comma_pos LT l.end_quote_pos ) AND ( l.comma_pos GT l.quote_pos ) }"> <MvCOMMENT> comma in a quoted string </MvCOMMENT>
			<MvASSIGN NAME = "l.comma_pos" VALUE = "{ indexof( ',', l.string, l.comma_pos + 1 ) }">
		</MvWHILE>
		
		<MvIF EXPR = "{ l.comma_pos EQ 0 }"> <MvCOMMENT> on the last parameter or we only have 1 parameter </MvCOMMENT>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ len_var( l.string ) - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ len_var( l.string ) }">
			<MvASSIGN NAME = "l.quote_pos" 		VALUE = 0>
		<MvELSE> <MvCOMMENT> if there are more commas to come, grab from the start to the comma</MvCOMMENT>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ l.comma_pos - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ l.comma_pos + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.param" VALUE = "{ trim( substring_var( l.string, l.start, l.end ) ) }">
		
		<MvIF EXPR = "{ ISNULL l.param }">
			<MvIF EXPR = "{ l.comma_pos EQ 0 AND l.parameter_count EQ 0 }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>
			
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Parameter number ' $ ( ++l.parameter_count ) $ ' cannot be empty' ) }">
		</MvIF>
		
		<MvASSIGN NAME = "l.parameters" INDEX = "{ ++l.parameter_count }" VALUE = "{ l.param }">
		
		<MvIF EXPR = "{ l.quote_pos EQ 0 AND l.comma_pos EQ 0 }">
			<MvWHILESTOP>
		</MvIF>

		<MvIF EXPR = "{ l.loop_counter GT 1000 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Error parsing parameters' ) }">
		</MvIF>
		
		<MvASSIGN NAME = "l.loop_counter" VALUE = "{ l.loop_counter + 1 }">
	</MvWHILE>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Parameter_Error" PARAMETERS = "param, message" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.error_count" 																VALUE = "{ miva_array_elements( g.Session:places_errors ) }">
	<MvASSIGN NAME = "g.Session:places_errors" INDEX = "{ l.error_count + 1 }" MEMBER = "param" 	VALUE = "{ l.param }">
	<MvASSIGN NAME = "g.Session:places_errors" INDEX = "{ l.error_count + 1 }" MEMBER = "message" 	VALUE = "{ l.message }">
	
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Is_Variable" PARAMETERS = "variable var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.valid_chars" 		VALUE = "_.: ">
	<MvASSIGN NAME = "l.square_bracket_pos" VALUE = 0>
	<MvASSIGN NAME = "l.bracket_count" 		VALUE = 0>

	<MvFOR INDEX = "l.pos" COUNT = "{ len_var( l.variable ) }">
		<MvASSIGN NAME = "l.char" VALUE = "{ substring_var( l.variable, l.pos, 1 ) }">

		<MvIF EXPR = "{ l.pos EQ 1 }">
			<MvIF EXPR = "{ ( NOT isalpha( l.char ) ) AND
							( l.char NE '_' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
			
			<MvFORCONTINUE>
		</MvIF>
		
		<MvIF EXPR = "{ l.char EQ '[' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count + 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos + 1 ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos ) }">
			
			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ ']' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count - 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos + 1 ) }">
			
			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ ( NOT isdigit( l.char ) ) AND
							( NOT isalpha( l.char ) ) AND
							( NOT ( l.char IN l.valid_chars ) ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOR>
	
	<MvIF EXPR = "{ l.bracket_count NE 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>
	
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>